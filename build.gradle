import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'net.neoforged.gradleutils' version '3.0.0-alpha.10'
    id 'signing'
}

group = 'net.neoforged'
gradleutils {
    version {
        branches.suffixBranch()
    }
    setupSigning(signAllPublications: true)
}
version = gradleutils.version
println "Version: $version"

repositories {
    mavenCentral()
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

license {
    header = file('LICENSE-header.txt')
    include 'net/neoforged/mergetool/**/*.java'
    newLine = false
}

sourceSets {
    cpwFML
    forgeFML
    forgeAPI
}

java {
    registerFeature('cpw') {
        usingSourceSet sourceSets.cpwFML
        capability(project.group, "mergetool-cpw", project.version.toString())
    }
    registerFeature('forge') {
        usingSourceSet sourceSets.forgeFML
        capability(project.group, "mergetool-forge", project.version.toString())
    }
    registerFeature('api') {
        usingSourceSet sourceSets.forgeAPI
        capability(project.group, "mergetool-api", project.version.toString())
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Main-Class': 'net.neoforged.mergetool.ConsoleMerger')
        attributes('Implementation-Version': project.version)
    }
    from sourceSets.cpwFML.output
    from sourceSets.forgeFML.output
    from sourceSets.forgeAPI.output
}

tasks.named('shadowJar', ShadowJar).configure {
    archiveClassifier = 'fatjar'
}

tasks.named('sourcesJar', Jar).configure {
    from sourceSets.cpwFML.allSource
    from sourceSets.forgeFML.allSource
    from sourceSets.forgeAPI.allSource
}

tasks.named('forgeAPIJar', Jar) {
    manifest.attributes('Automatic-Module-Name': 'net.neoforged.mergetool.api')
}

dependencies {
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'
    implementation 'org.ow2.asm:asm-util:9.5'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'net.neoforged:srgutils:1.0.10'

    implementation sourceSets.cpwFML.output
    implementation sourceSets.forgeFML.output
    implementation sourceSets.forgeAPI.output
}

[
        configurations.runtimeElements,
        configurations.apiElements,
        configurations.shadowRuntimeElements,
        configurations.sourcesElements
].each {
    it.outgoing {
        capability("${project.group}:mergetool:${project.version}")
        capability("${project.group}:mergetool-cpw:${project.version}")
        capability("${project.group}:mergetool-api:${project.version}")
        capability("${project.group}:mergetool-forge:${project.version}")
    }
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        artifactId = 'mergetool'

        pom {
            name = 'MergeTool'
            description = 'Merges two jar files together, useful for rebuilding Retroguard stripped jars.'
            url = 'https://github.com/neoforged/MergeTool'
            scm {
                url = 'https://github.com/neoforged/MergeTool'
                connection = 'scm:git:git://github.com/neoforged/MergeTool.git'
                developerConnection = 'scm:git:git@github.com:neoforged/MergeTool.git'
            }
            issueManagement {
                system = 'github'
                url = 'https://github.com/neoforged/MergeTool/issues'
            }

            licenses {
                license {
                    name = 'LGPLv2.1'
                    url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                }
            }

            developers {
                developer {
                    id = 'neoforged'
                    name = 'NeoForged'
                    email = 'contact@neoforged.net'
                    url = 'https://github.com/NeoForged/'
                }
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}

changelog {
    from '2.0'
}
